buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.24.2'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


def gitCommitId = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
def baseVersionName = '0.5.1'

android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "moe.shizuku.fcmformojo"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode gitCommitCount
        versionName "${baseVersionName}.r${gitCommitCount}.${gitCommitId}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resConfigs "en", "zh-rCN", "ja"

        resValue "string", "version_name", "${versionName}"
        buildConfigField "String", "DONATE_ALIPAY_URL", '"alipayqr://platformapi/startapp?saId=10000007&qrcode=https%3A%2F%2Fqr.alipay.com%2Faex01083scje5axcttivf13"'
        buildConfigField "String", "REQUIRE_SERVER_VERSION", '"0.5.1"'
    }
    signingConfigs {
        sign
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.sign

            ext {
                alwaysUpdateBuildId = false
                enableCrashlytics = false
            }
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.sign
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "ffm-${variant.versionCode}.apk"
        }
    }
}

configurations {
    compile.exclude group: 'com.android.support', module: 'support-v4'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    implementation "com.android.support:support-compat:$supportLibraryVersion"
    implementation "com.android.support:support-core-utils:$supportLibraryVersion"
    implementation "com.android.support:support-core-ui:$supportLibraryVersion"
    implementation "com.android.support:support-fragment:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "moe.shizuku.preference:preference:$preferenceLibraryVersion"
    implementation "moe.shizuku.preference:preference-dialog-android:$preferenceLibraryVersion"
    implementation "moe.shizuku.preference:preference-simplemenu:$preferenceLibraryVersion"
    implementation 'moe.shizuku.support:support-utils:2.0.3'
    implementation 'moe.shizuku.support:recyclerview-utils:2.0.3'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.google.firebase:firebase-messaging:11.6.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.3'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'moe.shizuku.fontprovider:api:8'
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }
    implementation "moe.shizuku.privilege:api-base:$shizukuPrivilegeBaseApiVersion"
    implementation "moe.shizuku.privilege:api-24:$shizukuPrivilegeApiVersion"
    implementation "moe.shizuku.privilege:api-25:$shizukuPrivilegeApiVersion"
    implementation "moe.shizuku.privilege:api-26:$shizukuPrivilegeApiVersion"
    implementation 'com.android.billingclient:billing:1.0'
}

def ksFile = rootProject.file('signing.properties')
def props = new Properties()
if (ksFile.canRead()) {
    props.load(new FileInputStream(ksFile))

    if (props != null) {
        android.signingConfigs.sign.storeFile file(props['KEYSTORE_FILE'])
        android.signingConfigs.sign.storePassword props['KEYSTORE_PASSWORD']
        android.signingConfigs.sign.keyAlias props['KEYSTORE_ALIAS']
        android.signingConfigs.sign.keyPassword props['KEYSTORE_ALIAS_PASSWORD']
    } else {
        println 'some entries in \'keystore.properties\' not found!'
    }
} else {
    println '\'keystore.properties\' not found!'
}

apply plugin: 'com.google.gms.google-services'